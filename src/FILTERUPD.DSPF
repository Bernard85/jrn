     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT(UL)
     A          R HDR1
     A                                      OVERLAY
     A            ZTL1         131A  O  1  2DSPATR(HI)
     A                                  3  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            ZCH1         125A  O  4  3COLOR(BLU)
     A            ZHDR1        125A  O  5  2DSPATR(HI)
     A          R SFL1                      SFL
     A*%%TS  SD  20190829  164117  BERNARD85   REL-V7R1M0  5770-WDS
     A  02                                  SFLNXTCHG
     A            XCHOICE1       1A  B  6  3CHECK(LC)
     A  02                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A            XFIL1        127A  O  6  6
     A          R CTL1                      SFLCTL(SFL1)
     A                                      CSRLOC(CSRTOROW1 CSRTOCOL1)
     A                                      CF01 CF02 CF03 CF04 CF05 CF06 CF07
     A                                      CF08 CF09 CF10 CF11 CF12 CF13 CF14
     A                                      CF15 CF16 CF17 CF18 CF19 CF20 CF21
     A                                      CF22 CF23 CF24 ROLLDOWN ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&SFL1CSRRRN)
     A  88                                  SFLDSP
     A N89                                  SFLDSPCTL
     A  89                                  SFLCLR
     A                                      SFLSIZ(20)
     A                                      SFLPAG(20)
     A            CSRTOROW1      3S 0H
     A            CSRTOCOL1      3S 0H
     A            SFL1CSRRRN     5S 0H
     A            SFL1RRN        4S 0H
     A            MYSFL1END      7A  O 26126DSPATR(HI)
     A            ZFK1         123A  O 26  2COLOR(BLU)
     A          R HDR2
     A                                      OVERLAY
     A            ZTL2         131A  O  1  2DSPATR(HI)
     A                                  3  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            ZCH2         125A  O  4  3COLOR(BLU)
     A            ZHDR2        125A  O  5  2DSPATR(HI)
     A          R SFL2                      SFL
     A  02                                  SFLNXTCHG
     A            XCHOICE2       1A  B  6  3CHECK(LC)
     A  02                                  DSPATR(RI)
     A  02                                  DSPATR(PC)
     A            XFIL2        127A  O  6  6
     A          R CTL2                      SFLCTL(SFL2)
     A                                      CSRLOC(CSRTOROW2 CSRTOCOL2)
     A                                      CF01 CF02 CF03 CF04 CF05 CF06 CF07
     A                                      CF08 CF09 CF10 CF11 CF12 CF13 CF14
     A                                      CF15 CF16 CF17 CF18 CF19 CF20 CF21
     A                                      CF22 CF23 CF24 ROLLDOWN ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&SFL2CSRRRN)
     A  88                                  SFLDSP
     A N89                                  SFLDSPCTL
     A  89                                  SFLCLR
     A                                      SFLSIZ(20)
     A                                      SFLPAG(20)
     A            CSRTOROW2      3S 0H
     A            CSRTOCOL2      3S 0H
     A            SFL2CSRRRN     5S 0H
     A            SFL2RRN        4S 0H
     A            MYSFL2END      7A  O 26126DSPATR(HI)
     A            ZFK2         123A  O 26  2COLOR(BLU)
     A          R MSGSFL                    SFL SFLMSGRCD(27)
     A            MSGSFLKEY                 SFLMSGKEY
     A            PGMID                     SFLPGMQ(10)
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      SFLDSP SFLDSPCTL SFLINZ
     A N98                                  SFLEND
     A                                      SFLPAG(1) SFLSIZ(2)
     A            PGMID                     SFLPGMQ(10)
