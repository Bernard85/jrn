     A*%%TS  SD  20190815  103504  BERNARD85   REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT(UL)
     A          R HDR1
     A*%%TS  SD  20190815  103504  BERNARD85   REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A            ZTL          131A  O  1  2DSPATR(HI)
     A                                  3  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A            ZCH          125A  O  4  3COLOR(BLU)
     A                                  5103'09876543210987654321098765432'
     A          R SFL1                      SFL
     A  02                                  SFLNXTCHG
     A  01        XFIL1        131A  O  6  2DSPATR(HI)
     A N01        XCHO           1A  B  6  3CHECK(LC)
     A  02                                  DSPATR(RI PC)
     A N01        XFIL         128A  O  6  5
     A          R CTL1                      SFLCTL(SFL1)
     A                                      CSRLOC(CSRTOROW CSRTOCOL)
     A                                      CF01 CF02 CF03 CF04 CF05 CF06 CF07
     A                                      CF08 CF09 CF10 CF11 CF12 CF13 CF14
     A                                      CF15 CF16 CF17 CF18 CF19 CF20 CF21
     A                                      CF22 CF23 CF24 ROLLDOWN ROLLUP
     A                                      OVERLAY
     A                                      SFLCSRRRN(&SFLCSRRRN)
     A  88                                  SFLDSP
     A N89                                  SFLDSPCTL
     A  89                                  SFLCLR
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0020)
     A            CSRTOROW       3S 0H
     A            CSRTOCOL       3S 0H
     A            SFLCSRRRN      5S 0H
     A            SFLRRN1        4S 0H
     A            MYSFLEND       7A  O 26126DSPATR(HI)
     A            ZFK          123A  O 26  2COLOR(BLU)
     A          R MSGSFL                    SFL SFLMSGRCD(27)
     A            MSGSFLKEY                 SFLMSGKEY
     A            PGMID                     SFLPGMQ(10)
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      SFLDSP SFLDSPCTL SFLINZ
     A N98                                  SFLEND
     A                                      SFLPAG(1) SFLSIZ(2)
     A            PGMID                     SFLPGMQ(10)
